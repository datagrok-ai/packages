#name: Bioreactor
#method: implicit
#description: Controlled fab-arm exchange mechanism simulation.
#tag: model
#differential equations:

  d(FFox)/dt = -FFox_to_FFred + FFred_to_FFox

  d(KKox)/dt = -KKox_to_KKred + KKred_to_KKox

  d(FFred)/dt = FFox_to_FFred - FFred_to_FFox - FFred_to_Ffree + Ffree_to_FFred

  d(KKred)/dt = KKox_to_KKred - KKred_to_KKox - KKred_to_Kfree + Kfree_to_KKred

  d(Ffree)/dt = 2.0 * FFred_to_Ffree - 2.0 * Ffree_to_FFred - free_to_FKred + FKred_to_free

  d(Kfree)/dt = 2.0 * KKred_to_Kfree - 2.0 * Kfree_to_KKred - free_to_FKred + FKred_to_free

  d(FKred)/dt = free_to_FKred - FKred_to_free - FKred_to_FKox + FKox_to_FKred

  d(FKox)/dt = FKred_to_FKox - FKox_to_FKred

  d(MEAthiol)/dt = 2.0 * (-FFox_to_FFred + FFred_to_FFox - KKox_to_KKred
			+ KKred_to_KKox + FFred_to_Ffree + KKred_to_Kfree - Ffree_to_FFred
			- Kfree_to_KKred - FKox_to_FKred
			- kthiolox * MEAthiol_t_by_Vres_t_squared * sqrt_of_Vres_t_by_CO2)
			- (MEAthiol + MEAthiolate) * (Fin + Fpermeate) / VL

  d(CO2)/dt = (Fin * CO2in - 2.0 * Fpermeate * CO2) / VL + OTR
	- 0.5 * kthiolox * MEAthiol_t_by_Vres_t_squared * sqrt_of_Vres_t_by_CO2
			
  d(yO2P)/dt = -OTR * (VL / Vg) * R * T * P + yO2in * qin - yO2P * qout

  d(CYST)/dt = kthiolox * MEAthiol_t_by_Vres_t_squared * sqrt_of_Vres_t_by_CO2
		- krcyst * CYST * Vres - (Fin + Fpermeate) * CYST / VL

  d(VL)/dt = Fin - Fpermeate

#expressions:

  constForklasurface = (3.932 * pow((pow(AgitatorSpeed, 3.0) * pow(AgitatorDiameter, 5.0) 
   * AgitatorPowerNumber / 2160000000000), 0.361)) / 60.0

  klasurface = pow(VL, -0.65) * constForklasurface

  MEAthiolate = MEAthiol * pow(10.0,(pH - pKa2MEA))

  qout = qin - klasurface*(yO2P*H - CO2) * VL * R * T / (P*1000.0)
  
  OTR = klasurface*(yO2P*H - CO2)

  Vg = Vtotalvessel - VL

  Fin = t < TimeToSwitch ? (0.0) : (0.025)

  Fpermeate = t < TimeToSwitch ? (0.025) : (Fin)
  
  CO2in = percentO2saturation * 7.17 / (32.0 * 100.0)

  Vres = VLinitial / VL

  MEAthiolate_t_by_Vres_t_squared = pow(MEAthiolate * Vres, 2.0)  
  
  FFox_to_FFred = k1red * FFox * Vres * MEAthiolate_t_by_Vres_t_squared

  FFred_to_FFox = k1ox * FFred * Vres

  FFred_to_Ffree = k2Fd * FFred * Vres

  Ffree_to_FFred = k2Fa * pow(Ffree * Vres, 2.0) * MEAthiolate_t_by_Vres_t_squared

  KKox_to_KKred = k1red * KKox * Vres * MEAthiolate_t_by_Vres_t_squared

  KKred_to_KKox = k1ox * KKred * Vres

  KKred_to_Kfree = k2Kd * KKred * Vres

  Kfree_to_KKred = k2Ka * pow(Kfree * Vres, 2.0) * MEAthiolate_t_by_Vres_t_squared

  free_to_FKred = k3FKa * Ffree * Vres * Kfree * Vres

  FKred_to_free = k3FKd * FKred * Vres

  FKred_to_FKox = k4ox * FKred * Vres * pow(CYST * Vres, 2.0)

  FKox_to_FKred = k4red * FKox * Vres * MEAthiolate_t_by_Vres_t_squared

  Vres_t_by_CO2 = Vres * CO2

  sqrt_of_Vres_t_by_CO2 = (Vres_t_by_CO2 >= 0.0) ? sqrt(Vres_t_by_CO2) : 0.0		

  MEAthiol_t_by_Vres_t_squared = pow(MEAthiol * Vres, 2.0)

#argument: t (time, min)
  initial = 0.0
  final = 1000.0
  step = 0.1

#initial values:  
  FFox = 0.2 (mmol/L) 
  KKox = 0.2 (mmol/L)
  FFred = 0.1 (mmol/L)
  KKred = 0.1 (mmol/L)
  Ffree = 0.0 (mmol/L)
  Kfree = 0.0 (mmol/L)
  FKred = 0.0 (mmol/L)
  FKox = 0.0 (mmol/L)
  MEAthiol = 15.0 (mmol/L)  
  CO2 = 0.12 (mmol O2/L)
  yO2P = 0.209 (ATM O2)
  CYST = 0.0 (mmol/L)
  VL = 7.2 (L) 

#constants:
  VLinitial = 7.2
  Vtotalvessel = 10.0
  AgitatorSpeed = 400.0
  AgitatorDiameter = 6.0
  AgitatorPowerNumber = 2.1
  pH = 7.4
  k1red =  0.05604
  k1ox = 0.0108
  k2Fd =  1.35
  k2Fa =  110400000.0
  k2Kd =  0.04038
  k2Ka =  120000000.0
  k3FKa =  181200000.0
  k3FKd =  0.01188
  k4ox =  0.0108
  k4red =  0.05604
  kthiolox = 0.005
  krcyst = 0.0
  percentO2saturation = 100.0
  pKa2MEA = 8.18

#parameters:
  qin = 1.0 (L/min)
  yO2in = 0.21 (mol)
  H = 1.072069378 (mmol O2/ L liquid /ATM O2)
  T = 300.0 (K)
  R = 0.082 (L ATM / mol K)
  P = 1.0 (ATM)
  TimeToSwitch = 135.0 (min)